
Fungible Token
============
#### ERC-20

목표
-------
1. Fungible Token과 ERC-20의 개념에 대해 배운다.
2. ERC-20 표준에 부합하는 스마트 컨트랙트를 배포해본다.
3. 기본적인 ERC20 토큰의 정보를 설정해 생성하고, 잔액 관리, 토큰 전송, 승인 및 허용량 관리, 승인된 토큰 전송 등을 수행하는 코드를 구현하고 실습한다.

개념
-------
Fungible Token은 대체 가능한 토큰으로, 각 토큰이 동일한 가치를 지니고 서로 교환이 가능하다. 이는 실물 화폐와 유사한 개념으로, 예를 들어 1달러 지폐는 다른 1달러 지폐와 완전히 대체 가능하다.

ERC-20은 이더리움 블록체인에서 Fungible Token을 위한 표준 프로토콜이다. 이 표준은 토큰의 전송, 잔액 조회, 총 공급량 확인 등의 기본적인 기능을 정의한다. ERC-20 표준은 토큰 간의 상호 운용성을 보장하며, 다양한 디앱(dApp)에서 사용된다.

ERC-20 토큰의 주요 특징은 다음과 같다:
- 모든 토큰은 동일한 가치를 가진다.
- 토큰은 분할 가능하다 (예: 0.5 토큰).
- 토큰 간 교환이 자유롭다.

미션 진행
------
### 1. license와 solidity 컴파일 버전 설정
> license: MIT
> 컴파일 버전: ^0.8.24

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;
```

### 2. IERC20 interface
IERC20는 ERC-20 표준에서 정의하는 기본적인 함수들을 선언한 인터페이스다.

```solidity
interface IERC20 {
    function totalSupply() external view returns (uint256);
    function balanceOf(address account) external view returns (uint256);
    function transfer(address recipient, uint256 amount) external returns (bool);
    function allowance(address owner, address spender) external view returns (uint256);
    function approve(address spender, uint256 amount) external returns (bool);
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender, uint256 value);
}
```


### 3. ERC20 컨트랙트
ERC20 컨트랙트는 IERC20 인터페이스를 구현한다.

```solidity
contract ERC20 is IERC20 {

    uint256 public totalSupply;
    mapping(address => uint256) public balanceOf;
    mapping(address => mapping(address => uint256)) public allowance;
    string public name;
    string public symbol;
    uint8 public decimals;

    constructor(string memory _name, string memory _symbol, uint8 _decimals) {
        name = _name;
        symbol = _symbol;
        decimals = _decimals;
    }

    function transfer(address recipient, uint256 amount) external returns (bool) {
        balanceOf[msg.sender] -= amount;
        balanceOf[recipient] += amount;
        emit Transfer(msg.sender, recipient, amount);
        return true;
    }

    function approve(address spender, uint256 amount) external returns (bool) {
        allowance[msg.sender][spender] = amount;
        emit Approval(msg.sender, spender, amount);
        return true;
    }

    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool) {
        allowance[sender][msg.sender] -= amount;
        balanceOf[sender] -= amount;
        balanceOf[recipient] += amount;
        emit Transfer(sender, recipient, amount);
        return true;
    }

    function _mint(address to, uint256 amount) internal {
        balanceOf[to] += amount;
        totalSupply += amount;
        emit Transfer(address(0), to, amount);
    }

    function _burn(address from, uint256 amount) internal {
        balanceOf[from] -= amount;
        totalSupply -= amount;
        emit Transfer(from, address(0), amount);
    }

    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }

    function burn(address from, uint256 amount) external {
        _burn(from, amount);
    }
}
```

- transfer
  토큰을 전송한다. 송신자의 잔액에서 차감하고 수신자의 잔액에 추가한다.

```solidity
function transfer(address recipient, uint256 amount) external returns (bool) {
    balanceOf[msg.sender] -= amount;
    balanceOf[recipient] += amount;
    emit Transfer(msg.sender, recipient, amount);
    return true;
}
```

- approve
  다른 주소에게 토큰 사용 권한을 부여한다.

```solidity
function approve(address spender, uint256 amount) external returns (bool) {
    allowance[msg.sender][spender] = amount;
    emit Approval(msg.sender, spender, amount);
    return true;
}
```

- transferFrom
  승인된 주소가 토큰을 전송한다. 승인량(allowance)을 차감한다.

```solidity
function transferFrom(address sender, address recipient, uint256 amount) external returns (bool) {
    allowance[sender][msg.sender] -= amount;
    balanceOf[sender] -= amount;
    balanceOf[recipient] += amount;
    emit Transfer(sender, recipient, amount);
    return true;
}
```

- _mint (내부 함수)
  새로운 토큰을 발행한다.

```solidity
function _mint(address to, uint256 amount) internal {
    balanceOf[to] += amount;
    totalSupply += amount;
    emit Transfer(address(0), to, amount);
}
```

- _burn (내부 함수)
  토큰을 소각한다.

```solidity
function _burn(address from, uint256 amount) internal {
    balanceOf[from] -= amount;
    totalSupply -= amount;
    emit Transfer(from, address(0), amount);
}
```

- mint
  외부에서 호출 가능한 토큰 발행 함수.

```solidity
function mint(address to, uint256 amount) external {
    _mint(to, amount);
}
```

- burn
  외부에서 호출 가능한 토큰 소각 함수.

```solidity
function burn(address from, uint256 amount) external {
    _burn(from, amount);
}
```

### 4. MyToken 컨트랙트
MyToken 컨트랙트는 ERC20을 상속받아 실제로 사용할 수 있는 토큰을 구현한다.

```solidity
contract MyToken is ERC20 {
    constructor(string memory name, string memory symbol, uint8 decimals)
        ERC20(name, symbol, decimals)
    {
        // 100 토큰을 컨트랙트 배포자(msg.sender)에게 발행
        // 1 토큰 = 1 * (10 ** decimals)
        _mint(msg.sender, 100 * 10 ** uint256(decimals));
    }
}
```

이 컨트랙트는 ERC20 컨트랙트를 상속받아 기본적인 토큰 기능을 모두 사용할 수 있다. 생성자에서는 이름, 심볼, 소수점 자리수를 설정하고, 초기에 100개의 토큰을 컨트랙트 배포자에게 발행한다.

실행 부분은 이전 설명과 동일하게 진행할 수 있으며, 초기 발행량이 100 토큰으로 설정되어 있음을 주의해야 한다.


전체 코드
-------


```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

interface IERC20 {
    function totalSupply() external view returns (uint256);
    function balanceOf(address account) external view returns (uint256);
    function transfer(address recipient, uint256 amount) external returns (bool);
    function allowance(address owner, address spender) external view returns (uint256);
    function approve(address spender, uint256 amount) external returns (bool);
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

contract ERC20 is IERC20 {

    uint256 public totalSupply;
    mapping(address => uint256) public balanceOf;
    mapping(address => mapping(address => uint256)) public allowance;
    string public name;
    string public symbol;
    uint8 public decimals;

    constructor(string memory _name, string memory _symbol, uint8 _decimals) {
        name = _name;
        symbol = _symbol;
        decimals = _decimals;
    }

    function transfer(address recipient, uint256 amount) external returns (bool) {
        balanceOf[msg.sender] -= amount;
        balanceOf[recipient] += amount;
        emit Transfer(msg.sender, recipient, amount);
        return true;
    }

    function approve(address spender, uint256 amount) external returns (bool) {
        allowance[msg.sender][spender] = amount;
        emit Approval(msg.sender, spender, amount);
        return true;
    }

    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool) {
        allowance[sender][msg.sender] -= amount;
        balanceOf[sender] -= amount;
        balanceOf[recipient] += amount;
        emit Transfer(sender, recipient, amount);
        return true;
    }

    function _mint(address to, uint256 amount) internal {
        balanceOf[to] += amount;
        totalSupply += amount;
        emit Transfer(address(0), to, amount);
    }

    function _burn(address from, uint256 amount) internal {
        balanceOf[from] -= amount;
        totalSupply -= amount;
        emit Transfer(from, address(0), amount);
    }

    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }

    function burn(address from, uint256 amount) external {
        _burn(from, amount);
    }
}

contract MyToken is ERC20 {
    constructor(string memory name, string memory symbol, uint8 decimals)
        ERC20(name, symbol, decimals)
    {
        // Mint 100 tokens to msg.sender
        // Similar to how
        // 1 dollar = 100 cents
        // 1 token = 1 * (10 ** decimals)
        _mint(msg.sender, 100 * 10 ** uint256(decimals));
    }
}
```

실행
------
Remix IDE를 사용해 ERC20 토큰 컨트랙트를 배포하고 테스트한다.

1. 준비
   - Remix IDE (https://remix.ethereum.org/)에 접속한다.
   - 'MyToken.sol' 파일을 생성하고 전체 코드를 붙여넣는다.
   - 컴파일러 버전(^0.8.24)을 확인하고 컴파일한다.

2. 컨트랙트 배포
   - 'Deploy & Run Transactions' 메뉴에서 'MyToken' 컨트랙트를 선택한다.
   - 다음 매개변수로 배포한다:
     name: "MyToken", symbol: "MTK", decimals: 18

3. 기본 정보 확인
   - 'name', 'symbol', 'decimals' 함수를 호출해 설정값을 확인한다.
   - 'totalSupply'를 호출해 초기 발행량(100 * 10^18)을 확인한다.
   - 배포자의 'balanceOf'를 확인해 초기 잔액을 확인한다.

![image](https://github.com/user-attachments/assets/14771144-ba75-4be3-af49-b38d61526ba6)


4. 토큰 전송 테스트
   - 'transfer' 함수로 10 토큰을 다른 주소로 전송한다.
   - 다른 주소 입력 방법:
     a) Remix 화면 상단의 'ACCOUNT' 드롭다운 메뉴를 클릭한다.
     b) 목록에서 현재 사용 중인 주소가 아닌 다른 주소를 선택한다.
     c) 선택한 주소를 복사한다.
   - 복사한 주소를 'transfer' 함수의 'recipient' 필드에 붙여넣는다.
   - 'amount' 필드에 10 * 10^18 (10 토큰)을 입력한다.
   - 'transact' 버튼을 클릭해 전송을 실행한다.
   - 송신자와 수신자의 'balanceOf'를 확인해 잔액 변화를 확인한다.

![image](https://github.com/user-attachments/assets/9d1b2f8f-2d57-459d-9b6d-b7613bf4f5eb)


5. 승인 및 위임 전송 테스트
   a) 승인 과정
      - 'approve' 함수를 찾는다.
      - 'spender' 필드에 승인할 주소를 입력한다 (4번에서 사용한 방법으로 다른 주소 선택).
      - 'amount' 필드에 20 * 10^18 (20 토큰)을 입력한다.
      - 'transact' 버튼을 클릭해 승인을 실행한다.

   b) 승인 확인
      - 'allowance' 함수를 찾는다.
      - 'owner' 필드에 현재 계정 주소를, 'spender' 필드에 승인받은 주소를 입력한다.
      - 'call' 버튼을 클릭해 승인된 금액을 확인한다. 20 * 10^18이 표시되어야 한다.

   c) 위임 전송 테스트
      - Remix 상단의 'ACCOUNT' 드롭다운에서 승인받은 주소로 전환한다.
      - 'transferFrom' 함수를 찾는다.
      - 'sender' 필드에 원래 토큰 소유자의 주소를 입력한다.
      - 'recipient' 필드에 또 다른 테스트 주소를 입력한다.
      - 'amount' 필드에 승인된 금액 이하 (예: 15 * 10^18)를 입력한다.
      - 'transact' 버튼을 클릭해 위임 전송을 실행한다.

   d) 결과 확인
      - 각 주소의 'balanceOf'를 호출해 잔액 변화를 확인한다.
      - 'allowance' 함수를 다시 호출해 남은 승인 금액을 확인한다.

![image](https://github.com/user-attachments/assets/86525124-2a11-4729-9170-196d76a0c637)
![image](https://github.com/user-attachments/assets/5869d888-f2b0-456f-8f40-6d64cd5c7a9e)



6. 토큰 발행 및 소각 테스트

   a) 토큰 발행 (Minting)
      - 'mint' 함수를 찾는다.
      - 'to' 필드에 토큰을 받을 주소를 입력한다 (자신의 주소나 다른 테스트 주소).
      - 'amount' 필드에 발행할 토큰 양을 입력한다 (예: 50 * 10^18 for 50 tokens).
      - 'transact' 버튼을 클릭해 새 토큰을 발행한다.
      - 'totalSupply' 함수를 호출해 전체 공급량이 증가했는지 확인한다.
      - 토큰을 받은 주소의 'balanceOf'를 호출해 잔액 증가를 확인한다.

   b) 토큰 소각 (Burning)
      - 'burn' 함수를 찾는다.
      - 'from' 필드에 토큰을 소각할 주소를 입력한다 (충분한 잔액이 있는 주소여야 함).
      - 'amount' 필드에 소각할 토큰 양을 입력한다 (예: 30 * 10^18 for 30 tokens).
      - 'transact' 버튼을 클릭해 토큰을 소각한다.
      - 'totalSupply' 함수를 다시 호출해 전체 공급량이 감소했는지 확인한다.
      - 토큰이 소각된 주소의 'balanceOf'를 호출해 잔액 감소를 확인한다.

   c) 주의사항
      - 발행과 소각 권한: 기본 구현에서는 누구나 mint와 burn 함수를 호출할 수 있다. 실제 사용 시에는 이 함수들에 접근 제어를 추가해야 한다.
      - 토큰 수량 입력: amount 필드에 입력할 때 토큰의 decimals를 고려해야 한다. 18 decimals의 경우, 1 토큰은 1 * 10^18이다.
      - 소각 시 잔액 확인: burn 함수 호출 전 해당 주소에 충분한 토큰이 있는지 확인해야 한다.

   d) 추가 테스트
      - 다양한 수량으로 발행과 소각을 테스트해본다.
      - 잔액이 부족한 상태에서 소각을 시도해 오류 처리를 확인한다.
      - 발행과 소각 후 이벤트 로그를 확인해 Transfer 이벤트가 제대로 발생했는지 확인한다.

![image](https://github.com/user-attachments/assets/82648dcb-cdee-480e-a347-e0f97397e9d8)
![image](https://github.com/user-attachments/assets/762f9201-6bb0-4b9a-8723-52764fa5f6eb)




추가 학습 : 토큰 스왑 컨트랙트
------

ERC20 토큰 간의 교환을 위한 `TokenSwap` 컨트랙트이다.
이 컨트랙트는 두 종류의 ERC20 토큰을 서로 교환하는 기능을 제공한다.

스왑 과정은 다음과 같이 `transferFrom` 함수를 호출하여 이루어진다:

```solidity
transferFrom(address sender, address recipient, uint256 amount)
```

이 함수는 `sender`로부터 `recipient`에게 `amount`만큼의 토큰을 전송한다.
`transferFrom`이 성공하기 위해서는 다음 조건이 충족되어야 한다:
* `sender`는 자신의 잔액에 `amount`보다 많은 토큰을 가지고 있어야 한다.
* `sender`는 `TokenSwap` 컨트랙트가 `amount`만큼의 토큰을 인출할 수 있도록 `approve` 함수를 미리 호출해야 한다.

기존의 ERC20 컨트랙트 코드 끝에 다음 TokenSwap 컨트랙트 코드를 추가한다:

```solidity
contract TokenSwap {
    IERC20 public token1;
    address public owner1;
    uint256 public amount1;
    IERC20 public token2;
    address public owner2;
    uint256 public amount2;

    constructor(
        address _token1,
        address _owner1,
        uint256 _amount1,
        address _token2,
        address _owner2,
        uint256 _amount2
    ) {
        token1 = IERC20(_token1);
        owner1 = _owner1;
        amount1 = _amount1;
        token2 = IERC20(_token2);
        owner2 = _owner2;
        amount2 = _amount2;
    }

    function swap() public {
        require(msg.sender == owner1 || msg.sender == owner2, "Not authorized");
        require(
            token1.allowance(owner1, address(this)) >= amount1,
            "Token 1 allowance too low"
        );
        require(
            token2.allowance(owner2, address(this)) >= amount2,
            "Token 2 allowance too low"
        );

        _safeTransferFrom(token1, owner1, owner2, amount1);
        _safeTransferFrom(token2, owner2, owner1, amount2);
    }

    function _safeTransferFrom(
        IERC20 token,
        address sender,
        address recipient,
        uint256 amount
    ) private {
        bool sent = token.transferFrom(sender, recipient, amount);
        require(sent, "Token transfer failed");
    }
}
```

Remix에서 TokenSwap 실습
------

1. 기존 MyToken.sol 파일에 TokenSwap 컨트랙트 코드를 추가한다.

2. 컴파일
   - 수정된 MyToken.sol 파일을 컴파일한다.

3. 토큰 컨트랙트 배포
   - MyToken 컨트랙트를 두 번 배포하여 두 개의 다른 토큰을 생성한다.
   - 첫 번째 배포: name="Token1", symbol="TK1", decimals=18
   - 두 번째 배포: name="Token2", symbol="TK2", decimals=18
   - 각 토큰의 주소를 기록해둔다.

4. TokenSwap 배포
   - TokenSwap 컨트랙트를 배포한다. 배포 시 다음 매개변수를 입력한다:
     - _token1: 첫 번째 MyToken 주소
     - _owner1: 첫 번째 토큰 소유자 주소
     - _amount1: 10 * 10^18 (10 Token1)
     - _token2: 두 번째 MyToken 주소
     - _owner2: 두 번째 토큰 소유자 주소
     - _amount2: 20 * 10^18 (20 Token2)

5. 승인 설정
   - 첫 번째 토큰 소유자 계정으로 첫 번째 MyToken의 approve 함수를 호출하여 TokenSwap Contract에 10 토큰 사용 권한을 부여한다.
   - 두 번째 토큰 소유자 계정으로 두 번째 MyToken의 approve 함수를 호출하여 TokenSwap Contract에 20 토큰 사용 권한을 부여한다.

6. 스왑 실행
   - 토큰 소유자 중 한 명의 계정으로 TokenSwap의 swap 함수를 호출한다.

7. 결과 확인
   - 각 MyToken의 balanceOf 함수를 호출하여 두 소유자의 잔액 변화를 확인한다.
