# Function Modifier

## 목표
- Solidity에서 Function Modifier의 개념과 사용법을 학습한다.
- Function Modifier를 사용하여 접근 제한, 입력 값 검증, 재진입 공격 방지 등의 기능을 구현하는 방법을 이해한다.
- 제공된 예제 코드를 Remix IDE에서 작성하고 실행하여 Function Modifier의 동작을 확인한다.

<br>
Solidity Smart Contract에서 Function Modifier는 함수 호출 전과 후에 실행될 수 있는 코드를 정의하는 데 사용된다. Function Modifier는 접근을 제한하거나, 입력값을 검증하거나, 재진입(reentrancy) 공격을 방지하는 등의 목적으로 활용될 수 있다.

## Function Modifier 예시

- **onlyOwner** : 함수가 호출될 때 호출자가 계약의 소유자인지 확인한다. `require(msg.sender == owner, "Not owner");` 문을 통해 호출자가 소유자가 아니면 함수를 중단하고 오류 메시지를 반환한다.
- **validAddress** : 입력된 주소가 유효한지 확인한다. `require(_addr != address(0), "Not valid address");` 문을 통해 주소가 0인 경우 함수를 중단하고 오류 메시지를 반환한다.
- **noReentrancy** : 재진입 공격을 방지한다. 함수가 실행 중일 때 `locked` 변수를 사용하여 재진입을 방지하고, 함수 실행이 끝나면 `locked` 변수를 초기화한다. 함수가 중복 실행되는 것을 방지할 수 있다.

## 예제 코드
소유자만 접근할 수 있는 기능, 주소 검증, 재진입 방지 기능을 구현한다.

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

contract FunctionModifier {
    // 이 변수들을 사용하여 함수 수정자의 사용법을 시연한다.
    address public owner;
    uint256 public x = 10;
    bool public locked;

    constructor() {
        // 계약의 소유자를 트랜잭션의 발신자로 설정한다.
        owner = msg.sender;
    }

    // 계약의 소유자인지 확인하는 수정자다.
    modifier onlyOwner() {
        require(msg.sender == owner, "Not owner");
        // 언더스코어(_)는 함수 수정자 내부에서만 사용되는 특별한 문자로,
        // Solidity에게 나머지 코드를 실행하도록 지시한다.
        _;
    }

    // 입력된 주소가 유효한지 확인하는 수정자입니다.
    modifier validAddress(address _addr) {
        require(_addr != address(0), "Not valid address");
        _;
    }

    // 소유자를 변경하는 함수입니다.
    // 이 함수는 소유자만 호출할 수 있으며, 유효한 주소가 입력되어야 한다.
    function changeOwner(address _newOwner)
        public
        onlyOwner
        validAddress(_newOwner)
    {
        owner = _newOwner;
    }

    // 함수가 실행 중일 때 재진입을 방지하는 수정자다.
    modifier noReentrancy() {
        require(!locked, "No reentrancy");

        locked = true;
        _;
        locked = false;
    }

    // 재진입 방지 기능이 적용된 함수다.
    function decrement(uint256 i) public noReentrancy {
        x -= i;

        if (i > 1) {
            decrement(i - 1);
        }
    }
}
```

### 예제 코드 설명

#### 상태 변수
- `address public owner;`: 계약의 소유자 주소를 저장한다.
- `uint256 public x = 10;`: 예제에서 사용할 정수형 변수다.
- `bool public locked;`: 재진입 방지를 위한 상태 변수다.

#### 생성자
```solidity
constructor() {
    owner = msg.sender;
}
```
- 계약의 소유자를 현재 트랜잭션을 보낸 주소로 설정한다.

#### onlyOwner 수정자
```solidity
modifier onlyOwner() {
    require(msg.sender == owner, "Not owner");
    _;
}
```
- 함수가 호출될 때 호출자가 소유자인지 확인한다. `msg.sender`는 트랜젝션(메세지) 발신자를 의미한다.
- Solidity의 Function Modifier에서 `_;`는 특별한 의미를 가지며, 수정된 함수의 본문이 실행되는 지점을 나타낸다. `_;`는 Function Modifier 내부에서 함수 본문이 실행될 위치를 지정한다. 이 위치에 따라 수정자의 코드를 함수 호출 전과 후에 실행할 수 있다.

#### validAddress 수정자
```solidity
modifier validAddress(address _addr) {
    require(_addr != address(0), "Not valid address");
    _;
}
```
- 입력된 주소가 유효한지 확인한다. Solidity에서 `address(0)은 주로 초기화 때 쓰는 값이다. 지금은 _addr에 초기화 값이 사용된게 아닌지 확인하는 것이다.

#### changeOwner 함수
```solidity
function changeOwner(address _newOwner)
    public
    onlyOwner
    validAddress(_newOwner)
{
    owner = _newOwner;
}
```
- 새로운 소유자를 설정하는 함수다. `onlyOwner`와 `validAddress` 수정자를 사용하여 접근을 제한하고 입력 값을 검증한다.

#### noReentrancy 수정자
```solidity
modifier noReentrancy() {
    require(!locked, "No reentrancy");

    locked = true;
    _;
    locked = false;
}
```
- 재진입 공격을 방지하기 위한 수정자다.

#### decrement 함수
```solidity
function decrement(uint256 i) public noReentrancy {
    x -= i;

    if (i > 1) {
        decrement(i - 1);
    }
}
```
- `x` 값을 감소시키는 함수다. `noReentrancy` 수정자를 사용하여 재진입을 방지한다.

## Remix에서 실습 
1. Remix에서 새로운 solidity 파일 생성해서 예제 코드를 복사 붙여넣기 한다.
2. 예제 코드를 compile 후 deploy한다.
3. 아래 버튼들이 제대로 동작하는지 확인한다.

- 초기 값입니다. <br/>
<img src= "https://github.com/Joon2000/Solidity-modules/blob/01752bdda7fa47e51918004ff029d72416de6492/images/functionmodifier/fminit.png" width="250px" height="300px" 
  title="fminit" alt="fminit"><br/>
- decrement를 진행한 경우, noReentrancy가 실행돼서 decremency를 못 한다는 log가 발생한다.
<img src= "https://github.com/Joon2000/Solidity-modules/blob/01752bdda7fa47e51918004ff029d72416de6492/images/functionmodifier/noreentrancy.png" width="1000px" height="100px" 
  title="noreentrancy" alt="noreentrancy"><br/>
- changeOwner owner가 아닌 주소로 실행하면 오류가 발생한다. owner로 실행하는 경우 정상적으로 작동한다.
<img src= "https://github.com/Joon2000/Solidity-modules/blob/01752bdda7fa47e51918004ff029d72416de6492/images/functionmodifier/changeowner.png" width="1000px" height="200px" 
  title="changeowner" alt="changeowner"><br/>
